reset
set terminal pngcairo size 1440,720 enhanced font 'Helvetica, 12'
set output sprintf( 'tpcc.'.ARG4.'.'.ARG3.'.png' )
set title "TPC-C ".ARG5." ".ARG3." threads" font ",14"
set grid
set xlabel "Time"
set ylabel "TPS"
set pointsize 0.5
set datafile sep ','
set key at graph 0.15, 0.1
f1 = ARG1
f2 = ARG2
stats f1 using 1:3 nooutput
f1maxy = STATS_max_y
maxx = STATS_max_x
stats f2 using 1:3 nooutput
f2maxy = STATS_max_y
if (f1maxy>f2maxy) maxy = f1maxy; else maxy = f2maxy;
set yrange [0:maxy+300]
set xrange [0:maxx+10]
set xtics time format "%tM"
stats f1 using 1:3 nooutput
set label 3 gprintf("m6gd.8xlarge Mean = %g TPS", STATS_mean_y) at graph 0.55, 0.080 front
set label 4 gprintf("m6gd.8xlarge Stddev = %g TPS", STATS_stddev_y) at graph 0.55, 0.050 front
stats f2 using 1:3 nooutput
set label 5 gprintf("m5d.8xlarge Mean = %g TPS", STATS_mean_y) at graph 0.77, 0.080 front
set label 6 gprintf("m5d.8xlarge Stddev = %g TPS", STATS_stddev_y) at graph 0.77, 0.050 front
plot f1 using 1:3 with points pt 7 lt rgb "#0a69a3" title "m6gd.8xlarge", \
     f2 using 1:3 with points pt 7 lt rgb "#e28743" title "m5d.8xlarge"

