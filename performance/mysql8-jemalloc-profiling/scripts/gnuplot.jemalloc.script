reset

set datafile sep ','

#connections = "16 64 128 256 512 1024 1536"
connections = "16 64 128 256 512 1024"
allocators = "glibc jemalloc360 jemalloc521 jemalloc521-profiling_22_28 jemalloc521-profiling_19_26"
tests = "oltp_read_only oltp_point_select"

do for [test in tests] {
  set print test.".dat"
  print 'Connections, glibc, jemalloc 3.6.0, jemalloc 5.2.1, jemalloc 5.2.1 prof 22/28, jemalloc 5.2.1 prof 19/26'
  set print test.".dat" append
  do for [cn in connections] {
    measurements=''
    do for [al in allocators] {
      file = system('ls -1B '.al.'.'.test.'.'.cn.'.csv')
      stats file using 1:3 nooutput
      measurements=measurements.','.sprintf("%f",STATS_mean_y)
    }
    print cn.measurements
  }
}

set style data histogram
set style histogram cluster gap 1
set style fill solid border rgb "black"
set auto x
set yrange [0:*]

set xlabel "Connections"
set ylabel "TPS"
set grid ytics

set terminal pngcairo size 1440,720 enhanced font 'Helvetica, 12'
set title "OLTP Point Select TPS" font ", 14"
set output 'oltp_point_select.png'

plot 'oltp_point_select.dat' using 2:xtic(1) title col, \
        '' using 3:xtic(1) title col, \
        '' using 4:xtic(1) title col, \
        '' using 5:xtic(1) title col, \
        '' using 6:xtic(1) title col

set terminal pngcairo size 1440,720 enhanced font 'Helvetica, 12'
set title "OLTP Read Only TPS" font ", 14"
set output 'oltp_read_only.png'

plot 'oltp_read_only.dat' using 2:xtic(1) title col, \
        '' using 3:xtic(1) title col, \
        '' using 4:xtic(1) title col, \
        '' using 5:xtic(1) title col, \
        '' using 6:xtic(1) title col


