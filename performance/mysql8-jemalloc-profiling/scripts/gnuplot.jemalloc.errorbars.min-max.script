reset

set datafile sep ','

connections = "16 64 128 256 512 1024"
allocators = "glibc-217 jemalloc360 jemalloc521 jemalloc521-profiling_22_28 jemalloc521-profiling_19_26"
tests = "oltp_read_only oltp_point_select"

do for [test in tests] {
  set print test.".dat"
  print 'Connections, glibc 2.17, min, max, jemalloc 3.6.0, min, max, jemalloc 5.2.1, min, max, jemalloc 5.2.1 prof 22/28, min, max, jemalloc 5.2.1 prof 19/26, min, max'
  set print test.".dat" append
  do for [cn in connections] {
    measurements=''
    do for [al in allocators] {
      file = system('ls -1B '.al.'.'.test.'.'.cn.'.csv')
      stats file using 1:3 nooutput
      measurements=measurements.','.sprintf("%f",STATS_mean_y).','.sprintf("%f",STATS_min_y).','.sprintf("%f",STATS_max_y)
    }
    print cn.measurements
  }
}

set style data histogram
set style histogram errorbars gap 1 lw 1.3
set style fill solid border rgb "black"
set auto x
set yrange [0:*]

set xlabel "Connections"
set ylabel "TPS"
set grid ytics

set terminal pngcairo size 1440,720 enhanced font 'Helvetica, 12'

set title "OLTP Point Select TPS with min-max bars" font ", 14"
set output 'oltp_point_select.min-max.png'

plot 'oltp_point_select.dat' using 2:3:4:xtic(1) title col, \
        '' using 5:6:7:xtic(1) title col, \
        '' using 8:9:10:xtic(1) title col, \
        '' using 11:12:13:xtic(1) title col, \
        '' using 14:15:16:xtic(1) title col

set title "OLTP Read Only TPS with min-max bars" font ", 14"
set output 'oltp_read_only.min-max.png'

plot 'oltp_read_only.dat' using 2:3:4:xtic(1) title col, \
        '' using 5:6:7:xtic(1) title col, \
        '' using 8:9:10:xtic(1) title col, \
        '' using 11:12:13:xtic(1) title col, \
        '' using 14:15:16:xtic(1) title col

